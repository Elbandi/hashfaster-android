buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.+'
    }
}
apply plugin: 'android'

dependencies {
    compile 'com.android.support:support-v4:18.0.+'
    compile files('libs/acra-4.5.0.jar', 'libs/core-2.3.0.jar', 'libs/litecoinwallet-integration-android-1.0.jar')
    compile project(':Libraries:ActionBar-PullToRefresh:library')
    compile project(':Libraries:ActionBar-PullToRefresh:extras:actionbarsherlock')
    compile project(':Libraries:ActionBarSherlock:actionbarsherlock')
    compile project(':Libraries:Android-ViewPagerIndicator:library')
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.0'

    defaultConfig {
        targetSdkVersion 19
        versionCode 4
        versionName "0.4"
    }

    signingConfigs {
        signing {
            if (project.hasProperty('storeFile') &&
                    project.hasProperty('storePassword') &&
                    project.hasProperty('keyAlias') &&
                    project.hasProperty('keyPassword')) {
                storeFile = file(project.storeFile)
                storePassword = project.storePassword
                keyAlias = project.keyAlias
                keyPassword = project.keyPassword
            }
        }
        devel {
            if (project.hasProperty('storeDebugFile')) {
                storeFile = file(project.storeDebugFile)
            }
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
    }

    buildTypes {
        release {
            debuggable false
            runProguard false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            if (project.hasProperty('storeFile')) {
                signingConfig signingConfigs.signing
            }
        }
        devel {
            debuggable true
            packageNameSuffix ".debug"
            versionNameSuffix "-debug"
            zipAlign true
            if (project.hasProperty('storeDebugFile')) {
                signingConfig signingConfigs.devel
            }
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }

    if (System.getenv("GIT_COMMIT")) defaultConfig.versionName += '-' + System.getenv("GIT_COMMIT").substring(0, 7);
    project.archivesBaseName += "-" + defaultConfig.versionName
}

task getVersion << {
  System.println android.defaultConfig.versionName
}

task getVersionCode << {
  System.println android.defaultConfig.versionCode
}

task release(dependsOn: assemble , type: Copy, description: "Copy packages to bin directory") {
    from('build/apk') {
        include '*.apk'
    }
    into file('../bin')
}

task releaseDevel(dependsOn: assembleDevel , type: Copy, description: "Copy packages to bin directory") {
    from('build/apk') {
        include '*.apk'
    }
    into file('../bin')
}
